plugins {
	id 'com.diffplug.blowdryer'
	id 'com.diffplug.spotless-changelog'
}

allprojects {
	apply from: 干.file('base/changelog.gradle')
}
apply from: 干.file('spotless/freshmark.gradle')

subprojects {
	apply from: 干.file('base/java8.gradle')
	if (it.name == 'spotless-changelog-lib') {
		ext.maven_name='Spotless Changelog Lib'
		ext.maven_desc='Utilities for parsing and updating a changelog'
		dependencies {
			compileOnly 'pl.tlinkowski.annotation:pl.tlinkowski.annotation.basic:0.2.0'
			testCompileOnly 'pl.tlinkowski.annotation:pl.tlinkowski.annotation.basic:0.2.0'
			implementation 'org.osgi:org.osgi.framework:1.9.0'
			implementation 'com.diffplug.durian:durian-core:1.2.0'
			implementation 'com.diffplug.durian:durian-collect:1.2.0'
			implementation 'com.diffplug.durian:durian-io:1.2.0'
			implementation "org.eclipse.jgit:org.eclipse.jgit:${VER_JGIT}"
			testImplementation 'junit:junit:4.12'
			testImplementation 'org.assertj:assertj-core:3.14.0'
		}
	} else {
		assert it.name == 'spotless-changelog-plugin-gradle'
		ext.maven_name='Spotless Changelog Gradle Plugin'
		ext.maven_desc='Gradle plugin for parsing and updating a changelog'

		apply from: 干.file('base/gradle-plugin.gradle')
		sourceSets {
			functionalTest {
				compileClasspath += sourceSets.main.compileClasspath
				runtimeClasspath += sourceSets.main.runtimeClasspath
			}
		}
		dependencies {
			implementation project(':spotless-changelog-lib')
			implementation 'com.diffplug.durian:durian-core:1.2.0'
			implementation 'com.diffplug.durian:durian-collect:1.2.0'
			implementation 'com.diffplug.durian:durian-io:1.2.0'
			implementation "org.eclipse.jgit:org.eclipse.jgit:${VER_JGIT}"
			testImplementation 'junit:junit:4.12'
			testImplementation 'org.assertj:assertj-core:3.14.0'
			// mock out the time
			implementation               'com.diffplug.durian-globals:durian-globals:1.0.0'
			functionalTestImplementation 'com.diffplug.durian-globals:durian-globals.dev:1.0.0'
		}
		gradlePlugin {
			pluginSourceSet project.sourceSets.functionalTest
		}
	}
	apply from: 干.file('base/maven.gradle')
	apply from: 干.file('base/bintray.gradle')
	apply from: 干.file('spotless/java.gradle')
}

ext.javadoc_agg='spotless-changelog-lib spotless-changelog-plugin-gradle'
apply from: 干.file('base/javadoc-agg.gradle')
